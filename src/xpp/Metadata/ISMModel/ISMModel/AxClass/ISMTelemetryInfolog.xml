<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ISMTelemetryInfolog</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Telemetry class for infolog messages
/// </summary>
class ISMTelemetryInfolog extends ISMTelemetryBase
{

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static ISMTelemetryInfolog construct()
    {
        return new ISMTelemetryInfolog();
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        super();

        this.eventId = ISMTelemetryEventIds::ISMEvent;
    }

]]></Source>
			</Method>
			<Method>
				<Name>shouldLogEvent</Name>
				<Source><![CDATA[
    protected boolean shouldLogEvent()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>postPopulateProperties</Name>
				<Source><![CDATA[
    protected void postPopulateProperties()
    {

    }

]]></Source>
			</Method>
			<Method>
				<Name>telemetryConfigurationName</Name>
				<Source><![CDATA[
    protected ClassName telemetryConfigurationName()
    {
        return classStr(ISMTelemetryInfologContract);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getContractDefiningMethodName</Name>
				<Source><![CDATA[
    public MethodName getContractDefiningMethodName()
    {
        return methodStr(ISMTelemetryInfolog, defineInfologContract);
    }

]]></Source>
			</Method>
			<Method>
				<Name>defineInfologContract</Name>
				<Source><![CDATA[
    /// <summary>
    /// Method signature provides the contract class to the SysOperation framework to 
    /// dynamically generate the configuration dialog.
    /// </summary>
    /// <param name = "_contract">Parameter to define the contract class</param>
    /// <remarks>
    /// Method is not called, only defines the contract class via its signature.
    /// </remarks>
    public void defineInfologContract(ISMTelemetryInfologContract _contract)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillConfigurationContract</Name>
				<Source><![CDATA[
    public void fillConfigurationContract(SysPackable _contract)
    {
        ISMTelemetryInfologContract contract;
        if (_contract && _contract is ISMTelemetryInfologContract)
        {
            contract = _contract as ISMTelemetryInfologContract;
            if (telemetryConfig != null)
            {
                contract.unpack(telemetryConfig.pack());
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setTelemetryConfiguration</Name>
				<Source><![CDATA[
    protected void setTelemetryConfiguration(SysPackable _config)
    {
        telemetryConfig = _config as ISMTelemetryInfologContract;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initializeParameterSetup</Name>
				<Source><![CDATA[
    protected void initializeParameterSetup(ISMTelemetryParameterSetup _parameterSetup)
    {
        _parameterSetup.TelemetryClass = this.telemetryName();
        _parameterSetup.IsEnabled = NoYes::No;
        _parameterSetup.SeverityLevel = ISMTelemetrySeverityLevel::Error;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initializeConfiguration</Name>
				<Source><![CDATA[
    protected void initializeConfiguration(SysPackable _configuration)
    {
        ISMTelemetryInfologContract contract = _configuration as ISMTelemetryInfologContract;
        if (contract)
        {
            contract.parmAddCallStackInformation(false);
            contract.parmAddCallStackWarning(false);
            contract.parmAddCallStackError(false);
            contract.parmCompressCallStack(true);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>info_onInfoLogMessageAdd</Name>
				<Source><![CDATA[
    /// <summary>
    /// Triggered when an infolog message is added.
    /// </summary>
    /// <param name = "_eventArgs">The event payload</param>
    [SubscribesTo(classStr(Info), delegateStr(Info, onInfoLogMessageAdd))]
    public static void info_onInfoLogMessageAdd(InfoLogMessageAddEventArgs _eventArgs)
    {
        ISMTelemetryInfolog infologTelemetry = new ISMTelemetryInfolog();
        infologTelemetry.processInfologAddEvent(_eventArgs);
    }

]]></Source>
			</Method>
			<Method>
				<Name>processInfologAddEvent</Name>
				<Source><![CDATA[
    /// <summary>
    /// Processes the event when an infolog message is added.
    /// </summary>
    /// <param name = "_eventArgs">The event payload</param>
    protected void processInfologAddEvent(InfoLogMessageAddEventArgs _eventArgs)
    {
        if (this.shouldSkipLogging())
        {
            // The logic before processEvent is heavy, so processing is skipped as early as possible.
            return;
        }

        // Event payload
        Exception infologLevel = _eventArgs.exception;
        str message = _eventArgs.message;
        str identifier = _eventArgs.identifier;

        ISMTelemetrySeverityLevel telemetrySeverityLevel =
            this.infologExceptionToTelemetrySeverityLevel(infologLevel);
        if (this.shouldSkipOnSeverityLevel(telemetrySeverityLevel))
        {
            // lets the user control which severity levels are logged
            return;
        }

        this.addRuntimeProperty(ISMTelemetryProperties::InfologMessage, message);
        this.addRuntimeProperty(ISMTelemetryProperties::InfologIdentifier, identifier);
        this.addRuntimeProperty(ISMTelemetryProperties::InfologLevel, enum2str(infologLevel));
        
        ISMTelemetryInfologContract contract = this.telemetryConfig;
        if (telemetrySeverityLevel == ISMTelemetrySeverityLevel::Information && contract.parmAddCallStackInformation()
            || telemetrySeverityLevel == ISMTelemetrySeverityLevel::Warning && contract.parmAddCallStackWarning()
            || telemetrySeverityLevel == ISMTelemetrySeverityLevel::Error && contract.parmAddCallStackError())
        {
            this.addCallStack(contract.parmCompressCallStack());
        }   

        this.processTrace(message, telemetrySeverityLevel);
    }

]]></Source>
			</Method>
			<Method>
				<Name>infologExceptionToTelemetrySeverityLevel</Name>
				<Source><![CDATA[
    /// <summary>
    /// Maps the infolog message level to the corresponding telemetry severity level.
    /// </summary>
    /// <param name = "_infologLevel">The infolog message level to map</param>
    /// <returns>The corresponding telemetry severity level or Verbose level.</returns>
    protected ISMTelemetrySeverityLevel infologExceptionToTelemetrySeverityLevel(Exception _infologLevel)
    {
        ISMTelemetrySeverityLevel severityLevel;
        switch (_infologLevel)
        {
            case Exception::Info:
                severityLevel = ISMTelemetrySeverityLevel::Information;
                break;
            case Exception::Warning:
                severityLevel = ISMTelemetrySeverityLevel::Warning;
                break;
            case Exception::Error:
                severityLevel = ISMTelemetrySeverityLevel::Error;
                break;
            default:
                severityLevel = ISMTelemetrySeverityLevel::Verbose;
                break;
        }
        return severityLevel;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>