<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ISMTelemetryConfigurationDialogController</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// SysOperation controller to display telemetry configuration dialogs.
/// </summary>
/// <remarks>
/// This is a bit of a hack of the SysOperation framework since the controller is not used to
/// execute any service. It is only used to make use of the framework's dialog generation capabilities.
/// </remarks>
class ISMTelemetryConfigurationDialogController extends SysOperationServiceController
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>canGoBatch</Name>
				<Source><![CDATA[
    public boolean canGoBatch()
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>telemetryConfigurationDialog</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method displays the telemetry configuration dialog for the provided telemetry instance.
    /// </summary>
    /// <param name = "_telemetry">The telemetry for which to show the configuration dialog.</param>
    /// <returns>True if the configuration was set successfully; otherwise, false.</returns>
    public static boolean telemetryConfigurationDialog(ISMTelemetryBase _telemetry)
    {
        ISMTelemetryConfigurationDialogController controller =
            new ISMTelemetryConfigurationDialogController(
                classId2Name(classIdGet(_telemetry)), 
                _telemetry.getContractDefiningMethodName(),
                SysOperationExecutionMode::Synchronous);
        controller.parmShowDialog(true);
        controller.parmShowProgressForm(false);
        _telemetry.fillConfigurationContract(controller.getDataContractObject());
        controller.parmDialogCaption(_telemetry.configurationDescription());
        controller.parmLoadFromSysLastValue(false);
        if (controller.prompt())
        {
            _telemetry.setConfigurationFromContract(controller.getDataContractObject());
            return true;
        }
        return false;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>